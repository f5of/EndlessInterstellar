import java.text.DateFormat
import java.text.SimpleDateFormat
import java.util.function.Consumer

// root gradle file by nekit508

allprojects { Project project ->
    project.plugins.apply("java")
    project.plugins.apply("idea")
    project.plugins.apply("maven-publish")

    version = "vT0.3.1"
    group = "github.f5of.endless-interstellar"

    tasks.withType(JavaCompile).configureEach {
        options.forkOptions.jvmArgs += "-Xms256m -Xmx1024m --illegal-access=permit" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.jvm=ALL-UNNAMED" +
                "--add-opens=jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED" +
                "--add-opens=java.base/sun.reflect.annotation=ALL-UNNAMED"
    }
}

wrapper {
    gradleVersion = "8.1.1"
}

sourceSets.main.java.srcDirs = []
sourceSets.main.resources.srcDirs = []

task cleanBuilds(type: Delete) {
    doLast {
        subprojects.each { Project p ->
            if (p.buildDir.exists()) {
                delete(p.buildDir)
            }
        }
    }
}

ext {
    localProperties = new Properties()
    if (new File("$rootProject.projectDir/local.properties").exists())
        localProperties.load(new FileInputStream(new File("$rootProject.projectDir/local.properties")))

    setIfHasProp = { String property, Consumer<Object> set ->
        if (localProperties.containsKey(property))
            set.accept(localProperties.get(property))
    }
    doExec = { String cmd ->
        var proc = cmd.execute(null, rootProject.projectDir)
        proc.waitForProcessOutput(System.out, System.err)
    }
}

subprojects { Project project ->
    project.sourceSets.main.java.srcDirs = [
            "src", "gen"
    ]
    project.sourceSets.main.resources.srcDirs = [
            "res",
    ]

    project.ext {
        APArgs = ["ProjectName=$project.name", "OutputPackage=f5of.ei.files"]

        copyPaths = [
                "$rootProject.projectDir/artifacts",
        ]
        mindustryVersion = "v143.1"

        child = { File p, String c ->
            return new File(p, c)
        }

        writeProcessors = {
            var processorPath = child(project.sourceSets.main.resources.srcDirs.getAt(0), "META-INF/services/")
            processorPath.mkdirs()
            var processorFile = child(processorPath, ("javax.annotation.processing.Processor"))
            var text = new StringBuilder()
            var srcFiles = project.sourceSets.main.java.srcDirs.getAt(0)
            srcFiles.eachFileRecurse(groovy.io.FileType.FILES){ file ->
                if(file.name.endsWith(".java") && ((file.text.contains(" extends AbstractProcessor")
                        && !file.text.contains("abstract class")))){
                    text.append(file.path.substring(srcFiles.path.length() + 1)).append("\n")
                }
            }
            processorFile.text = text.toString().replace(".java", "").replace("/", ".").replace("\\", ".")
        }
    }

    project.repositories {
        mavenCentral()
        maven { url "https://www.jitpack.io" }
    }

    project.tasks.withType(JavaCompile).configureEach {
        options.generatedSourceOutputDirectory = project.file("gen")
        rootProject.ext.setIfHasProp("org.gradle.java.home", v -> options.forkOptions.executable = v)
        options.encoding = "UTF-8"
        sourceCompatibility = JavaVersion.VERSION_16
        targetCompatibility = JavaVersion.VERSION_16
        var finalAPArgs = []
        APArgs.each { String str ->
            if (!str.contains("-A"))
                finalAPArgs.add("-A" + str)
        }
        options.compilerArgs += finalAPArgs

        doFirst {
            delete options.generatedSourceOutputDirectory.get().asFile.listFiles()
        }
    }

    project.tasks.create("cjar") {
        dependsOn project.tasks.jar
        finalizedBy rootProject.tasks.cleanBuilds
    }

    project.tasks.jar {
        from {
            configurations.runtimeClasspath.collect { File file ->
                file.isDirectory() ? file : zipTree(file)
            }
        }
        doLast {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            copy {
                from {
                    archiveFile.get()
                }
                copyPaths.each { p ->
                    into(p)
                }
            }
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

task publishProj {
    doLast {
        publishToMavenLocal
    }
}

task deploy {
    dependsOn subprojects.stream().<Task>map(p -> p.tasks.jar).toArray()
    finalizedBy rootProject.tasks.cleanBuilds
}

task workflow {
    if (wType == "push") {
        dependsOn deploy
    } else if (wType == "pull-request") {
        dependsOn deploy
    }

    doLast {
        if (wType == "push") {
            String date = new SimpleDateFormat("yyyy-MM-dd-HH:mm:ss").format(Calendar.getInstance().getTime());
            doExec("git tag -a $date -m Tag \"Created by workflow.\"")
            doExec("git push --tags")

        } else if (wType == "pull-request") {

        }
    }
}
